dnl configure.in for xcin
dnl
dnl Process this file with autoconf to produce a configure script.
dnl-----------------------------------------------------------------------
AC_INIT(src/xcin_main.c)
AC_CONFIG_AUX_DIR(script)
AC_CONFIG_HEADER(config.h:script/config.h.in)
AC_CANONICAL_HOST

dnl-----------------------------------------------------------------------
dnl Obtain xcin version information.
dnl-----------------------------------------------------------------------
verf='src/include/constant.h'
RELEASE_VER=`awk '{if(/XCIN_VERSION/) print $3}' $verf | sed 's/"\(.*\)"/\1/'`
CURRENT_VER=`awk '{if(/CURRENT_VER/) print $3}' $verf`
REVISION_VER=`awk '{if(/REVISION_VER/) print $3}' $verf`
AGE_VER=`awk '{if(/AGE_VER/) print $3}' $verf`
AC_SUBST(RELEASE_VER)
AC_SUBST(CURRENT_VER)
AC_SUBST(REVISION_VER)
AC_SUBST(AGE_VER)
AC_MSG_RESULT([Configuring for XCIN $RELEASE_VER ....])

dnl-----------------------------------------------------------------------
dnl xcin configure command line options.
dnl-----------------------------------------------------------------------
AC_ARG_ENABLE(debug,
    [  --enable-debug            enable debug],
    [  test x$enableval = xyes && AC_DEFINE(DEBUG) ])

AC_ARG_WITH(xcin_rcdir,
    [  --with-xcin-rcdir=PATH    set the xcin rcfile path.], ,
    [  with_xcin_rcdir=$sysconfdir ])

AC_ARG_WITH(xcin_dir,
    [  --with-xcin-dir=PATH      set the xcin main path.], ,
    [  with_xcin_dir=$libdir/xcin ])

AC_ARG_WITH(locale_dir,
    [  --with-locale-dir=PATH    set the locale path (for LC_MESSAGES).], ,
    [  with_locale_dir=$datadir/locale ])

AC_ARG_WITH(man_dir,
    [  --with-man_dir=PATH       set the man-page path.], ,
    [  with_man_dir=$mandir ])

AC_ARG_WITH(extra_prefix,
    [  --with-extra-prefix=PATH  set the extra prefix for external packages.])

AC_ARG_WITH(extra_inc,
    [  --with-extra-inc=PATH     set the extra include dir.],
    [  test x$withval != x && with_extra_inc="-I$withval" ])

AC_ARG_WITH(extra_lib,
    [  --with-extra-lib=PATH     set the extra library dir.])

AC_ARG_WITH(db_inc,
    [  --with-db-inc=PATH        set the include dir of Berkeley DB2/3.],
    [  test x$withval != x && with_db_inc="-I$withval" ],
    [  with_db_inc='' ])

AC_ARG_WITH(db_lib,
    [  --with-db-lib=PATH        set the library dir of Berkeley DB2/3.],
    [  test x$withval != x && test_db_lib="-L$withval" ],
    [  test_db_lib='' ])

AC_ARG_WITH(tabe_inc,
    [  --with-tabe-inc=PATH      set the include dir of libtabe.],
    [  test x$withval != x && with_tabe_inc="-I$withval" ],
    [  with_tabe_inc='' ])

AC_ARG_WITH(tabe_lib,
    [  --with-tabe-lib=PATH      set the library dir of libtabe.],
    [  test x$withval != x && test_tabe_lib="-L$withval" ],
    [  test_tabe_lib='' ])

AC_ARG_WITH(intl_inc,
    [  --with-intl-inc=PATH      set the include dir of libintl (gettext).],
    [  test x$withval != x && with_intl_inc="-I$withval" ],
    [  with_intl_inc='' ])

AC_ARG_WITH(intl_lib,
    [  --with-intl-lib=PATH      set the library dir of libintl (gettext).],
    [  test x$withval != x && test_intl_lib="-L$withval" ],
    [  test_intl_lib='' ])

AC_ARG_WITH(iconv_inc,
    [  --with-iconv-inc=PATH     set the include dir of iconv header file.],
    [  test x$withval != x && with_iconv_inc="-I$withval" ],
    [  with_iconv_inc='' ])

AC_ARG_WITH(iconv_lib,
    [  --with-iconv-lib=PATH     set the include dir of iconv library.],
    [  test x$withval != x && test_iconv_lib="-L$withval" ],
    [  test_iconv_lib='' ])

dnl-----------------------------------------------------------------------
dnl Setting default options
dnl-----------------------------------------------------------------------
if test x$prefix = x || test $prefix = NONE; then
    prefix=/usr/local
fi
if test x$exec_prefix = x || test $exec_prefix = NONE; then
    exec_prefix="$prefix"
fi
if test x$program_prefix = x || test $program_prefix = NONE; then
    program_prefix=''
else
    program_prefix="$program_prefix/"
fi

if test x$with_extra_prefix = x; then
    with_extra_prefix=$prefix
fi
if test x$with_extra_inc = x; then
    with_extra_inc="-I$with_extra_prefix/include"
fi
if test x$with_extra_lib = x; then
    with_extra_lib="$with_extra_prefix/lib"
fi

AC_SUBST(program_prefix)
AC_SUBST(with_xcin_rcdir)
AC_SUBST(with_xcin_dir)
AC_SUBST(with_locale_dir)
AC_SUBST(with_man_dir)

incs="$with_extra_inc $with_db_inc $with_tabe_inc $with_intl_inc \
      $with_iconv_inc"
libs="-L$with_extra_lib $test_db_lib $test_tabe_lib $test_intl_lib \
      $test_iconv_lib"

dnl-----------------------------------------------------------------------
dnl Check compiler: You can specify in 'CC=<..> ./configure'
dnl-----------------------------------------------------------------------
OLD_CFLAGS="$CFLAGS"
AC_PROG_CC
if test "x$GCC" != xyes; then
    compiler_type=

    AC_MSG_CHECKING([ANSI C compiler])
    CFLAGS="$OLDCFLAGS -Aa"
    AC_TRY_RUN([ int main(){return 0;} ], [ compiler_type=ANSIC ],
	       [ compiler_type=UNKNOWN ], [ cross_compiler=1 ])
    if test $compiler_type = ANSIC; then
	AC_MSG_RESULT([yes])
    else
	AC_MSG_RESULT([no])
    fi
fi
CFLAGS="$OLD_CFLAGS"

dnl-----------------------------------------------------------------------
dnl Supply default CFLAGS, if not specified by `CFLAGS=flags ./configure'
dnl-----------------------------------------------------------------------
if test -z "$CFLAGS"; then
    if test "x$GCC" = xyes; then
	optimize_opt=-O2
	CFLAGS='-Wall -fsigned-char'
    else
	optimize_opt=-O
	if test "x$compiler_type" = xANSIC; then
	    CFLAGS=-Aa
	else
	    CFLAGS=
	fi
    fi
    if test "x$enable_debug" = xyes; then
	CFLAGS="$CFLAGS -g"
    else
	CFLAGS="$CFLAGS $optimize_opt"
    fi
fi

dnl-----------------------------------------------------------------------
dnl libtool commands.
dnl-----------------------------------------------------------------------
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

dnl-----------------------------------------------------------------------
dnl Checks for Host type.
dnl-----------------------------------------------------------------------
case "$host_os" in
    linux*)  
	os_type=LINUX
        AC_DEFINE(LINUX)
	;;
    freebsd*)
	os_type=FREEBSD
        AC_DEFINE(FREEBSD)
	;;
    hp*)
	os_type=HPUX
        AC_DEFINE(HPUX)
	;;
    openbsd*)
	os_type=OPENBSD
	AC_DEFINE(OPENBSD)
	;;
    netbsd*)
	os_type=NETBSD
	AC_DEFINE(NETBSD)
	;;
    solaris*)
	os_type=SOLARIS
	AC_DEFINE(SOLARIS)
	;;
    *)
	AC_MSG_RESULT([Warning: xcin does not support this system yet.])
	AC_MSG_RESULT([Warning: please report this to xcin@linux.org.tw])
	os_type=UNKNOWN
	;;
esac
AC_SUBST(os_type)

dnl-----------------------------------------------------------------------
dnl Checks for programs.
dnl-----------------------------------------------------------------------
AC_PROG_CPP
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_PROG_INSTALL

${INSTALL} -d testdir > /dev/null 2>&1
if test ! -d testdir; then
    INSTALL="$srcdir/script/install-sh -c"
    AC_SUBST(INSTALL)
fi
rm -rf testdir
AC_MSG_RESULT([checking for install -d: $INSTALL])

AC_CHECK_PROG(PERL, perl, perl, sh $(noperl))
AC_SUBST(PERL)

dnl-----------------------------------------------------------------------
dnl Checks for header files.
dnl-----------------------------------------------------------------------
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_MAJOR
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h \
		limits.h \
		malloc.h \
		sys/ioctl.h \
		sys/time.h \
		unistd.h \
		wchar.h)

dnl-----------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl-----------------------------------------------------------------------
AC_C_CONST
AC_C_LONG_DOUBLE
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_ST_BLKSIZE
AC_STRUCT_ST_BLOCKS
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_STRUCT_TM

AC_CHECK_SIZEOF(char,      [ 1 ])
AC_CHECK_SIZEOF(short,     [ 2 ])
AC_CHECK_SIZEOF(int,       [ 4 ])
AC_CHECK_SIZEOF(long,      [ 4 ])
AC_CHECK_SIZEOF(long long, [ 8 ])
AC_CHECK_SIZEOF(int *,     [ 4 ])

dnl-----------------------------------------------------------------------
dnl Checks for library functions.
dnl-----------------------------------------------------------------------
AC_FUNC_FNMATCH
AC_FUNC_GETPGRP
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_UTIME_NULL
AC_FUNC_VPRINTF

AC_CHECK_FUNCS(select, ,
	[ AC_MSG_RESULT([select() does not supported in your system.])
	  exit 1 ])

AC_CHECK_FUNCS(getcwd \
dnl	gethostname \
	snprintf \
	gettimeofday \
	mkdir \
	mktime \
	putenv \
	rmdir \
	strcspn \
	strdup \
	strerror \
	strspn \
	strstr \
	strtol \
	strtoul \
	uname \
	nl_langinfo)

dnl-----------------------------------------------------------------------
dnl Set include and library paths.
dnl-----------------------------------------------------------------------
OLD_CPPFLAGS=$CPPFLAGS
OLD_LDFLAGS=$LDFLAGS
OLD_LIBS=$LIBS
CPPFLAGS="$OLD_CPPFLAGS $incs"
LDFLAGS="$OLD_LDFLAGS $libs"

dnl-----------------------------------------------------------------------
dnl Checks for Locale related library.
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
AC_CHECK_LIB(xpg4, setlocale,
	     [ with_locale_lib=-lxpg4 ], [ with_locale_lib="" ])
AC_SUBST(with_locale_lib)

dnl-----------------------------------------------------------------------
dnl Checks for dlopen related library.
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
AC_CHECK_FUNCS(dlopen, [ found=y ], [ found=n ])
if test $found = n; then
    AC_CHECK_LIB(dl, dlopen,
	[ found=y; with_dl_lib=-ldl; AC_DEFINE(HAVE_DLOPEN) ], [ found=n ])
fi

if test $found = n; then
    AC_CHECK_FUNCS(shl_load, [ found=y ], [ found=n ])
fi
if test $found = n; then
    for f in dl dld; do
	AC_CHECK_LIB($f, shl_load,
	    [ found=y; with_dl_lib="-l$f"
	      AC_DEFINE(HAVE_SHL_LOAD) ], [ found=n ])
	test $found = y && break
    done
fi
AC_SUBST(with_dl_lib)

dnl-----------------------------------------------------------------------
dnl Checks for socket library
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
AC_CHECK_FUNCS(socket, [ found=y; with_socket_lib='' ], [ found=n ])
if test $found = n; then
    AC_CHECK_LIB(socket, socket, [ with_socket_lib=-lsocket ], 
	[ AC_MSG_RESULT([Your system does not support socket to compile xcin.])
	  exit 1 ])
fi
AC_SUBST(with_socket_lib)

dnl-----------------------------------------------------------------------
dnl Checks for gettext.
dnl-----------------------------------------------------------------------
AC_CHECK_PROG(GNU_GETTEXT, gettext, 1, 0)
OLD_GNU_GETTEXT=0
if test x$GNU_GETTEXT = x1; then
    need_gettext_ver1=0
    need_gettext_ver2=10
    need_gettext_ver3=36
    need_gettext_ver=$need_gettext_ver1.$need_gettext_ver2.$need_gettext_ver3

    gettext_ver=`xgettext -V | awk '{if(NR==1) print $NF}'`
    gettext_ver1=`echo $gettext_ver | awk 'BEGIN{FS="."} {print $1}'`
    gettext_ver2=`echo $gettext_ver | awk 'BEGIN{FS="."} {print $2}'`
    gettext_ver3=`echo $gettext_ver | awk 'BEGIN{FS="."} {print $3}'`
    res1=`expr $gettext_ver1 - $need_gettext_ver1`
    case $res1 in
    -*) OLD_GNU_GETTEXT=1
	;;
     0) res2=`expr $gettext_ver2 - $need_gettext_ver2`
	case $res2 in
	-*) OLD_GNU_GETTEXT=1
	    ;;
	 0) res3=`expr $gettext_ver3 - $need_gettext_ver3`
	    case $res3 in
	    -*) OLD_GNU_GETTEXT=1
		;;
	    esac
	    ;;
	esac
	;;
    esac
    AC_MSG_RESULT([GNU Gettext found version $gettext_ver.])
dnl if test x$GNU_GETTEXT = x0; then
dnl	AC_MSG_RESULT([however, version $need_gettext_ver or above is needed.])
dnl fi
fi
AC_SUBST(GNU_GETTEXT)
AC_SUBST(OLD_GNU_GETTEXT)

LIBS="$OLD_LIBS"
AC_CHECK_HEADERS(libintl.h, [ found=y ], [ found=n ])
if test $found = y; then
    AC_CHECK_FUNCS(gettext, [ found=y ], [ found=n ])
    if test $found = n; then
	AC_CHECK_LIB(intl, gettext,
	    [ found=y; AC_DEFINE(HAVE_GETTEXT)
	      test_intl_lib="$test_intl_lib -lintl" ],
	    [ test_intl_lib='' ])
    fi
fi

dnl-----------------------------------------------------------------------
dnl Checks for iconv() function/library.
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
AC_CHECK_HEADERS(iconv.h, ,
    [ AC_MSG_RESULT([iconv.h is not available.])
      AC_MSG_RESULT([Please use --with-iconv-inc to configure.])
      exit 1 ])

AC_CHECK_FUNCS(iconv, [ found=y ], [ found=n ])
if test $found = n; then
    for f in x iconv giconv; do
	if test $f != x; then
	    LIBS="$OLD_LIBS -l$f"
	    AC_MSG_CHECKING([iconv in -l$f])
	else
	    AC_MSG_CHECKING([iconv in standard libc])
	fi

	AC_TRY_LINK([
#include <stdlib.h>
#include <iconv.h>], [
iconv_t cd = iconv_open("","");
iconv(cd,NULL,NULL,NULL,NULL);
iconv_close(cd);], [ found=y; AC_DEFINE(HAVE_ICONV)
		     test_iconv_lib="$test_iconv_lib -l$f" ], [ found=n ])
	if test $found = y; then
	    AC_MSG_RESULT([yes])
	    break
	else
	    AC_MSG_RESULT([no])
	fi
    done
fi
if test $found = n; then
    AC_MSG_RESULT([iconv() is not supported in your system.])
    AC_MSG_RESULT([Please use --with-iconv-lib to configure.])
    exit 1
fi

dnl-----------------------------------------------------------------------
dnl Checks for Berkeley DB2/3 library.
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
for f in db db3; do
    AC_CHECK_LIB($f, db_create,
	[ found=y; test_db_lib="$test_db_lib -l$f" ], [ found=n ])
    test $found = y && break
done
if test $found = n; then
    for f in db db2; do
	AC_CHECK_LIB($f, db_open,
	    [ found=y; test_db_lib="$test_db_lib -l$f" ], [ found=n ])
	test $found = y && break
    done
fi
if test $found = n; then
    AC_MSG_RESULT([You need Berkeley DB2/3 to compile bimsphone module.])
    AC_MSG_RESULT([Please use --with-db-lib to configure.])
    exit 1
fi

dnl-----------------------------------------------------------------------
dnl Checks for X11R6 library
dnl-----------------------------------------------------------------------
LIBS="$OLD_LIBS"
AC_PATH_X
if test x$x_libraries != x; then
    LDFLAGS="$LDFLAGS -L$x_libraries"
fi
if test x$x_includes != x; then
    x_includes="-I$x_includes"
fi
AC_CHECK_LIB(X11, XmbDrawImageString, ,
    [ AC_MSG_RESULT([You need X11R6 to compile xcin.])
      AC_MSG_RESULT([Please use --x-includes and --x-libraries to configure.])
      exit 1 ])

LDFLAGS="$OLD_LDFLAGS $libs"

dnl-----------------------------------------------------------------------
dnl Checks for tabe & bims libraries.
dnl-----------------------------------------------------------------------
AC_MSG_CHECKING([tabe data files])
if test x$with_tabe_lib = x; then
    tabe_data="$with_extra_prefix/share/tabe/tsiyin/"
else
    tabe_data=`echo $with_tabe_lib | \
	sed 's|^\(.*\)/lib$|\1/share/tabe/tsiyin/|'`
fi
if test ! -f $tabe_data/tsi.db; then
    tabe_data=''
    AC_MSG_RESULT([not found])
else
    AC_MSG_RESULT([$tabe_data])
fi
AC_SUBST(tabe_data)

LIBS="$OLD_LIBS"
CPPFLAGS="$CPPFLAGS $x_includes"
AC_CHECK_LIB(tabe, tabeZhiInfoLookupYin, , 
    [ AC_MSG_RESULT([You need libtabe to compile bimsphone module.])
      AC_MSG_RESULT([Please use --with-tabe-lib to configure.])
      exit 1], [ $test_db_lib ])

AC_CHECK_HEADERS(bims.h, ,
    [ AC_MSG_RESULT([bims.h is not available.])
      AC_MSG_RESULT([Please use --with-tabe-inc to configure.])
      exit 1 ])

AC_CHECK_LIB(bims, bimsInit, ,
    [ AC_MSG_RESULT([You need libbims to compile bimsphone module.])
      AC_MSG_RESULT([Please use --with-tabelib to configure.])
      exit 1], [ -ltabe $test_db_lib ])
test_tabe_lib="$test_tabe_lib -lbims -ltabe"

dnl-----------------------------------------------------------------------
dnl Set proper paths of libraries.
dnl-----------------------------------------------------------------------
with_extra_lib="-L$with_extra_lib -rpath $with_extra_lib"

if test x$x_libraries = x; then
    x_libraries="-lX11 -lXext"
else
    x_libraries="-L$x_libraries -lX11 -lXext -rpath $x_libraries"
fi
if test x$with_db_lib = x; then
    with_db_lib="$test_db_lib"
else
    with_db_lib="$test_db_lib -rpath $with_db_lib"
fi
if test x$with_tabe_lib = x; then
    with_tabe_lib="$test_tabe_lib"
else
    with_tabe_lib="$test_tabe_lib -rpath $with_tabe_lib"
fi
if test x$with_intl_lib = x; then
    with_intl_lib="$test_intl_lib"
else
    with_intl_lib="$test_intl_lib -rpath $with_intl_lib"
fi
if test x$with_iconv_lib = x; then
    with_iconv_lib="$test_iconv_lib"
else
    with_iconv_lib="$test_iconv_lib -rpath $with_iconv_lib"
fi

AC_SUBST(with_extra_inc)
AC_SUBST(with_extra_lib)
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(with_db_inc)
AC_SUBST(with_db_lib)
AC_SUBST(with_tabe_inc)
AC_SUBST(with_tabe_lib)
AC_SUBST(with_intl_inc)
AC_SUBST(with_intl_lib)
AC_SUBST(with_iconv_inc)
AC_SUBST(with_iconv_lib)

dnl-----------------------------------------------------------------------
dnl Output Makefiles and config.h
dnl-----------------------------------------------------------------------
CPPFLAGS=$OLD_CPPFLAGS
LIBS=$OLD_LIBS

eval xcin_rcp=$with_xcin_rcdir
eval xcin_p=$with_xcin_dir
eval xcin_msgp=$with_locale_dir
AC_DEFINE_UNQUOTED(XCIN_DEFAULT_RCDIR,	"${xcin_rcp}")
AC_DEFINE_UNQUOTED(XCIN_DEFAULT_DIR,	"${xcin_p}")
AC_DEFINE_UNQUOTED(XCIN_MSGLOCAT,	"${xcin_msgp}")

AC_OUTPUT(Makefile:script/Makefile.in \
	Rules:script/Rules.in \
	src/Makefile \
	src/lib/Makefile \
 	src/lib/xcintool/Makefile \
	src/lib/charset/Makefile \
	src/lib/siod/Makefile \
	src/lib/IMdkit/Makefile \
	src/Cinput/Makefile \
	src/Cinput/im_comm/Makefile \
	src/Cinput/zh_hex/Makefile \
	src/Cinput/gen_inp/Makefile \
	src/Cinput/bimsphone/Makefile \
	src/util/Makefile \
	src/util/cin2tab/Makefile \
	src/util/testprog/Makefile \
	cin/Makefile \
	cin/big5/Makefile \
	cin/gb/Makefile \
	etc/Makefile \
	po/Makefile)
